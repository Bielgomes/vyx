PROGRAM -> DECLARATION* EOF
DECLARATION -> LETDECL | STATEMENT
LETDECL -> "let" IDENTIFIER ( "=" EXPRESSION )? ";"
STATEMENT -> EXPRSTMT | IFSTMT | PRINTSTMT | WHILESTMT | FORSTMT | BLOCK
FORSTMT -> "for" "(" ( LETDECL | EXPRSTMT | ";" ) EXPRESSION? ";" EXPRESSION? ")" STATEMENT
WHILESTMT -> "while" "(" EXPRESSION ")" STATEMENT
IFSTMT -> "if" "(" EXPRESSION ")" STATEMENT ( "else" STATEMENT )?
BLOCK -> "{" DECLARATION "}"
EXPRSTMT -> EXPRESSION ";"
PRINTSTMT -> "print(" EXPRESSION ");"

EXPRESSION -> ASSIGNMENT;
ASSIGNMENT -> IDENTIFIER "=" ASSIGNMENT | ELVIS
ELVIS -> TERNARY ( "?:" TERNARY )*
TERNARY -> LOGICALOR ( "?" EXPRESSION ":" TERNARY )?
LOGICALOR -> LOGICALAND ( "or" LOGICALAND )*
LOGICALAND -> EQUALITY ( "and" EQUALITY )*
EQUALITY -> COMPARISON ( ( "!=" | "==" COMPARISON ) COMPARISON )*
COMPARISON -> TERM ( ( ">" | "<" | ">=" | "<=" ) TERM )*
TERM -> FACTOR ( ( "+" | "-" ) FACTOR )*
FACTOR -> UNARY ( ( "/" | "*" ) UNARY )*
UNARY -> ( "!" | "-" ) UNARY | CALL
CALL -> PRIMARY ( "(" ARGUMENTS? ")" )*
ARGUMENTS -> EXPRESSION ( "," EXPRESSION )*
PRIMARY -> "true" | "false" | "null" | NUMBER | STRING | "(" EXPRESSION ")" | IDENTIFIER